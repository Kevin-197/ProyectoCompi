package compi;

import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.io.FileWriter;
import java.io.IOException;

parser code
{:
    private ArrayList<Symbol> s = new ArrayList<Symbol>();
    
    public void syntax_error(Symbol s){
        this.s.add(s);
    }

    public ArrayList<Symbol> getS(){
        return this.s;
    }
    
    protected final static int _error_sync_size = 3;
    protected int error_sync_size( ) {
        return _error_sync_size;
    }

    public TablaSimbolos tabla = TablaSimbolos.getInstance();
    public PilaSemantica pila = PilaSemantica.getInstance();
    public String rutaEnsamblador = "D:/RepoVR/ProyectoCompi/Compi/src/compi/archivoAnalisis.asm";
    public int contador = 0;
    public int contadorBreak = 0;
    public int contadorContinue = 0;
    
    public void GuardarRS_Tipo(String Tipo){
        RS_Tipo tipo = new RS_Tipo(Tipo);
        this.pila.Push(tipo);
    }

    public void GuardarRS_Var(String Id, int Line, int Column){
        RS_Var var = new RS_Var(Id,Line,Column);
        this.pila.Push(var);
    }
    
    public void GuardarRS_Param(String Id, int Line, int Column){
        RS_Param param = new RS_Param(Id,Line,Column);
        this.pila.Push(param);
    }

    public void BuscarSimbolo(String Id, int Line, int Column, String Ambito){
        RegistroTS a = this.tabla.BuscarSimboloDeclarado(Id, Ambito);
        if(a==null){
            System.out.println("\u001B[31mError semantico. Linea: " + Line + " Columna: " + Column +  " No ha sido declarado el identificador \""+Id+"\"\u001B[31m");
        }
    }
    
    public void ValidarLlamadaFunc(String Id, int Line, int Column){
        this.pila.ValidarLlamadaFunc(Id, Line, Column);
    }

    public Boolean init = true; 
    public Boolean initFunc = true; 
    public void GuardarDeclaracionTS(String Ambito){
        if(Ambito.equals("Funcion") && initFunc == true){
            try{
                FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
                escritorEnsamblador.write("\nsection .text"+  "\nglobal _start\n_start:\n\n");
                escritorEnsamblador.close();
            }
            catch (IOException e){
                e.printStackTrace();
            }
            initFunc = false;
        }
        if(init == true){
            try{
                FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
                escritorEnsamblador.write("section .data"+  "\n");
                escritorEnsamblador.close();
            }
            catch (IOException e){
                e.printStackTrace();
            }
            init = false;
        }
        this.pila.GuardarVariable(Ambito);
    }

    public void StartWhile(){
        this.contadorBreak++;
        this.contadorContinue++;
        try{
            FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
            String label1 =  "while_label" + Integer.toString(this.contador);
            String label2 =  "exit_label" + Integer.toString(this.contador); 
            RS_While rs_while = new RS_While(label1,label2);
            escritorEnsamblador.write("\n"+rs_while.label1 + ":" + "\n");
            escritorEnsamblador.close();
            this.pila.Push(rs_while);
            this.contador++;
        }
        catch (IOException e){
            e.printStackTrace();
        }
    }

    public void TestWhile(){
        try{
            FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
            RS_DO exp = (RS_DO) this.pila.Pop();
            RS_While rs_while = pila.BuscarRS_While();
            escritorEnsamblador.write(exp.Valor+" " + rs_while.label2 + "\n");
            escritorEnsamblador.close();
        }
        catch (IOException e){
            e.printStackTrace();
        }
    }

    public void EndWhile(){
        this.contadorBreak--;
        this.contadorContinue--;
        try{
            FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
            RS_While rs_while = pila.BuscarRS_While();
            escritorEnsamblador.write("jmp " + rs_while.label1 + "\n");
            escritorEnsamblador.write("\n"+rs_while.label2 + ":" + "\n");
            escritorEnsamblador.close();
            this.pila.EndWhile();
        }
        catch (IOException e){
            e.printStackTrace();
        }
    }

    public void ValidarBreak(int Line, int Column){
        if(contadorBreak <= 0){
            System.out.println("\u001B[31mError semantico. Linea: " + Line + " Columna: " + Column +  " El break no se encuentra en una estructura de control valida\u001B[31m");
        }
    }
    
    public void ValidarContinue(int Line, int Column){
        if(contadorContinue <= 0){
            System.out.println("\u001B[31mError semantico. Linea: " + Line + " Columna: " + Column +  " El continue no se encuentra en una estructura de control valida\u001B[31m");
        }
    }

    public void StartIf(String Else_label, String Exit_label){
        RS_If if_else = new RS_If(Else_label,Exit_label,contador);
        this.pila.Push(if_else);
        this.contador++;
    }

    public void TestIf(){
        try{

            FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
            RS_DO exp = (RS_DO) this.pila.Pop();
            RS_If if_else = this.pila.BuscarRSIf();
            escritorEnsamblador.write(exp.Valor+" " + if_else.Else_label + "\n");
            escritorEnsamblador.close();
        }
        catch (IOException e){
            e.printStackTrace();
        }
    }

    public void StartElse(){
        try{
            FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
            RS_If if_else = this.pila.BuscarRSIf();
            escritorEnsamblador.write("jmp " + if_else.Exit_label + "\n");
            escritorEnsamblador.write("\n"+if_else.Else_label + ":" + "\n");
            escritorEnsamblador.close();
        }
        catch (IOException e){
            e.printStackTrace();
        }
    }

    public void RecuerdaConstante(String Valor, String Tipo){
        int val = 0;
        if(Tipo=="Numero"){
           val = Integer.parseInt(Valor);
        }
        else if(Tipo=="Char"){
           val = Valor.toString().charAt(1);
        } 
        RS_DO rs_do = new RS_DO(Tipo,val+"");
        this.pila.Push(rs_do);
    }

    public void AddPrefix(String val){
        this.pila.AddPrefix(val);
    }
    
    public void RecuerdaOP(String Valor){
        RS_Operador rs_op = new RS_Operador(Valor);
        this.pila.Push(rs_op);
    }
    public void RecuerdaVariable(String Valor, String Tipo){ 
        RS_DO rs_do = new RS_DO(Tipo,"["+Valor+"]");
        this.pila.Push(rs_do);
    }
    public void EvalOP(){ 
        this.pila.EvalOP();
    }
    public void EvalOPUnaria(int op){ 
        this.pila.EvalOPUnaria(op);
    }

    public void EndIf(){
        try{
            FileWriter escritorEnsamblador = new FileWriter(rutaEnsamblador,true);
            RS_If if_else = this.pila.BuscarRSIf();
            escritorEnsamblador.write("\n"+if_else.Exit_label + ":" + "\n");
            escritorEnsamblador.close();
            this.pila.EndIf();
        }
        catch (IOException e){
            e.printStackTrace();
        }
    }

:};

terminal Define, Write, Read, Break, Case, Char, Const, Continue, Default, Do, Else, For,
    If, Integer, Long, Return, Short, Switch, Void, While, OperadorSuma, 
    OperadorResta, OperadorMulti, OperadorDiv, OperadorMod, 
    OperadorAsignacionSuma, OperadorAsignacionResta, OperadorAsignacionMul,
    OperadorAsignacionDiv, OperadorAsignacion, OperadorIncremento, OperadorDecremento, 
    OperadorComparacion, OperadorMenor, OperadorMayor, OperadorMayorIgual, OperadorMenorIgual,
    OperadorDiferencia, OperadorNegacion, OperadorAnd, OperadorOr, 
    ParentesisA, ParentesisC, LlaveA, LlaveC, Coma, PuntoComa, DosPuntos, Int, Identificador, LiteralChar, ERROR;

non terminal FUNCION, SENTENCIA, PARAMETROS, OPERADOR_ARITMETICO2, OPERADOR_ARITMETICO1, 
    DATO_PREFIX, OPERADOR_BOOL, OPERADOR_OR_AND, OPERADOR_ASIG, TIPO_DATO, DATO,
    DECLARACION_LIST, DECLARACION, OPERACION, OPERACION1, OPERACION2, FACTOR, EXPRESION, IF, IF_ELSE,
    INSTRUCCIONES_CONTROL, WHILE, DO_WHILE, SENTENCIA_BOOLEANA, SENTENCIA_FOR, LISTA_EXPRESIONES, FOR,
    READ, WRITE, LISTA_IDENTIFICADORES, INICIO, INICIO_B, SWITCH, ETIQUETA_SWITCH, OPERACION_SWITCH1,
    OPERACION_SWITCH2, CONSTANT_FACTOR, NOMBRE_FUNCION, TIPO_DATO_FUNCION;

start with INICIO;

INICIO ::= 
    Define Identificador EXPRESION INICIO |
    Define Identificador EXPRESION PuntoComa INICIO |
    DECLARACION {: parser.GuardarDeclaracionTS("Global"); :} INICIO |
    FUNCION |
    error LlaveC INICIO_B |
    error PuntoComa INICIO
;
INICIO_B ::= 
    Define Identificador EXPRESION INICIO_B  |
    Define Identificador EXPRESION PuntoComa INICIO_B  |
    DECLARACION {: parser.GuardarDeclaracionTS("Global"); :} INICIO_B |
    Define Identificador EXPRESION |
    Define Identificador EXPRESION PuntoComa |
    DECLARACION {: parser.GuardarDeclaracionTS("Global"); :} |
    FUNCION |
    error LlaveC INICIO_B |
    error PuntoComa INICIO_B
;
FUNCION ::= 
    TIPO_DATO NOMBRE_FUNCION ParentesisA PARAMETROS ParentesisC LlaveA SENTENCIA LlaveC {: parser.tabla.TerminaFuncion(); :} |
    TIPO_DATO_FUNCION NOMBRE_FUNCION ParentesisA PARAMETROS ParentesisC LlaveA SENTENCIA LlaveC {: parser.tabla.TerminaFuncion(); :} |
    TIPO_DATO NOMBRE_FUNCION ParentesisA PARAMETROS ParentesisC LlaveA SENTENCIA LlaveC {: parser.tabla.TerminaFuncion(); :} INICIO_B |
    TIPO_DATO_FUNCION NOMBRE_FUNCION ParentesisA PARAMETROS ParentesisC LlaveA SENTENCIA LlaveC {: parser.tabla.TerminaFuncion(); :} INICIO_B 
;
TIPO_DATO_FUNCION ::= 
    Void {: parser.GuardarRS_Tipo("void"); :}
;
NOMBRE_FUNCION ::=
    Identificador:v1 {: parser.GuardarRS_Var(v1.toString(),v1right +1,v1left +1);
    parser.GuardarDeclaracionTS("Funcion"); :}
; 
SENTENCIA ::= 
    SENTENCIA DECLARACION {: parser.GuardarDeclaracionTS("Local"); :} |
    DECLARACION {: parser.GuardarDeclaracionTS("Local"); :} |
    SENTENCIA EXPRESION PuntoComa |
    EXPRESION PuntoComa |
    SENTENCIA INSTRUCCIONES_CONTROL |
    INSTRUCCIONES_CONTROL |
    SENTENCIA READ |
    READ |
    SENTENCIA WRITE |
    WRITE |
    SENTENCIA IF | 
    IF |
    SENTENCIA IF_ELSE |
    IF_ELSE |
    SENTENCIA WHILE |
    WHILE |
    SENTENCIA DO_WHILE |
    DO_WHILE |
    SENTENCIA FOR |
    FOR |
    SENTENCIA SWITCH |
    SWITCH |
    SENTENCIA Define Identificador EXPRESION PuntoComa |
    Define Identificador EXPRESION PuntoComa |
    error PuntoComa |
    error LlaveC | 
    error ParentesisC
;
PARAMETROS ::= 
    TIPO_DATO Identificador:v1 {: parser.GuardarRS_Var(v1.toString(),v1right +1,v1left +1);
    parser.GuardarDeclaracionTS("Parametro"); :}  |
    PARAMETROS Coma TIPO_DATO Identificador:v1 {: parser.GuardarRS_Var(v1.toString(),v1right +1,v1left +1);
    parser.GuardarDeclaracionTS("Parametro"); :} |
;
INSTRUCCIONES_CONTROL ::= 
    Return EXPRESION PuntoComa |
    Return PuntoComa |
    Break PuntoComa:v1 {: parser.ValidarBreak(v1right +1,v1left +1); :} |
    Continue PuntoComa:v1 {: parser.ValidarContinue(v1right +1,v1left +1); :}
;
OPERADOR_ARITMETICO2 ::= 
    OperadorMulti:v1 {: parser.RecuerdaOP(v1.toString()); :} |
    OperadorDiv:v1 {: parser.RecuerdaOP(v1.toString()); :}  | 
    OperadorMod:v1 {: parser.RecuerdaOP(v1.toString()); :} 
;
OPERADOR_ARITMETICO1 ::= 
    OperadorSuma:v1 {: parser.RecuerdaOP(v1.toString()); :}  | 
    OperadorResta:v1 {: parser.RecuerdaOP(v1.toString()); :}  
;
DATO_PREFIX ::= 
    OperadorSuma:v1 {: RESULT=v1.toString(); :} | 
    OperadorResta:v1 {: RESULT=v1.toString(); :} |
    OperadorNegacion:v1 {: RESULT=v1.toString(); :} 
;
OPERADOR_BOOL ::= 
    OperadorComparacion:v1 {: parser.RecuerdaOP(v1.toString()); :}  |
    OperadorMenor:v1 {: parser.RecuerdaOP(v1.toString()); :}  | 
    OperadorMayor:v1 {: parser.RecuerdaOP(v1.toString()); :}  | 
    OperadorMayorIgual:v1 {: parser.RecuerdaOP(v1.toString()); :}  | 
    OperadorMenorIgual:v1 {: parser.RecuerdaOP(v1.toString()); :}  |
    OperadorDiferencia:v1 {: parser.RecuerdaOP(v1.toString()); :}
;
OPERADOR_OR_AND ::= 
    OperadorOr:v1 {: parser.RecuerdaOP(v1.toString()); :}  |
    OperadorAnd:v1 {: parser.RecuerdaOP(v1.toString()); :}  
;
OPERADOR_ASIG ::= 
    OperadorAsignacion:v1 {: parser.RecuerdaOP(v1.toString()); :}  |
    OperadorAsignacionSuma:v1 {: parser.RecuerdaOP(v1.toString()); :}  | 
    OperadorAsignacionResta:v1 {: parser.RecuerdaOP(v1.toString()); :}  | 
    OperadorAsignacionMul:v1 {: parser.RecuerdaOP(v1.toString()); :}  |
    OperadorAsignacionDiv:v1 {: parser.RecuerdaOP(v1.toString()); :}  
;
TIPO_DATO ::= 
    Char {: parser.GuardarRS_Tipo("char"); :} |
    Integer {: parser.GuardarRS_Tipo("int"); :} | 
    Long {: parser.GuardarRS_Tipo("long"); :} | 
    Short {: parser.GuardarRS_Tipo("short"); :} |
    Short Integer {: parser.GuardarRS_Tipo("short int"); :} | 
    Long Integer {: parser.GuardarRS_Tipo("long int"); :}
;
DATO ::= 
    LiteralChar:v1 {: parser.RecuerdaConstante(v1.toString(),"Char"); :} |
    Int:v1 {: parser.RecuerdaConstante(v1.toString(),"Numero"); :} |
    Identificador:v1 {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.RecuerdaVariable(v1.toString(),"Direccion"); :}
;
DECLARACION_LIST ::= 
    Identificador:v1 {: parser.GuardarRS_Var(v1.toString(),v1right +1,v1left +1); :} |
    DECLARACION_LIST Coma Identificador:v1 {: parser.GuardarRS_Var(v1.toString(),v1right +1,v1left +1); :} |
    Identificador:v1 OperadorAsignacion OPERACION {: parser.GuardarRS_Var(v1.toString(),v1right +1,v1left +1); :} |
    DECLARACION_LIST Coma Identificador:v1 OperadorAsignacion OPERACION {: parser.GuardarRS_Var(v1.toString(),v1right +1,v1left +1); :}
;
DECLARACION ::= 
    TIPO_DATO DECLARACION_LIST PuntoComa |
    TIPO_DATO Const DECLARACION_LIST PuntoComa |  
    Const TIPO_DATO DECLARACION_LIST PuntoComa
;
OPERACION ::= 
    OPERACION OPERADOR_BOOL OPERACION1 {: parser.EvalOP(); :} | 
    OPERACION1
;
OPERACION1 ::= 
    OPERACION1 OPERADOR_ARITMETICO1 OPERACION2 {: parser.EvalOP(); :} | 
    OPERACION2
;
OPERACION2 ::= 
    OPERACION2 OPERADOR_ARITMETICO2 FACTOR {: parser.EvalOP(); :} | 
    FACTOR
;
FACTOR ::= 
    ParentesisA EXPRESION ParentesisC | 
    DATO_PREFIX ParentesisA EXPRESION ParentesisC | 
    DATO_PREFIX:v1 DATO {: parser.AddPrefix(v1.toString()); :} |
    DATO |
    OperadorDecremento Identificador:v1 {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.RecuerdaVariable(v1.toString(),"Direccion"); parser.EvalOPUnaria(2); :} |
    OperadorIncremento Identificador:v1 {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.RecuerdaVariable(v1.toString(),"Direccion"); parser.EvalOPUnaria(1); :} |
    Identificador:v1 OperadorDecremento {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.RecuerdaVariable(v1.toString(),"Direccion"); parser.EvalOPUnaria(2); :} |
    Identificador:v1 OperadorIncremento {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.RecuerdaVariable(v1.toString(),"Direccion"); parser.EvalOPUnaria(1); :} |
    Identificador:v1 ParentesisA LISTA_IDENTIFICADORES ParentesisC {: parser.ValidarLlamadaFunc(v1.toString(),v1right +1,v1left +1); :} |
    Identificador:v1 ParentesisA ParentesisC {: parser.ValidarLlamadaFunc(v1.toString(),v1right +1,v1left +1); :}
;
EXPRESION ::= 
    Identificador:v1 {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.RecuerdaVariable(v1.toString(),"Direccion"); :} OPERADOR_ASIG OPERACION {: parser.EvalOP(); :} |
    OPERACION 
;
READ ::= Read ParentesisA ParentesisC PuntoComa |
    Read ParentesisA LISTA_IDENTIFICADORES ParentesisC PuntoComa
;
WRITE ::= Write ParentesisA LISTA_EXPRESIONES ParentesisC PuntoComa
;
IF ::= If {: parser.StartIf("else_label","exit_label"); :} ParentesisA SENTENCIA_BOOLEANA ParentesisC {: parser.TestIf(); :} LlaveA SENTENCIA LlaveC
;
SENTENCIA_BOOLEANA ::= 
    OPERACION |
    Identificador:v1 {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.RecuerdaVariable(v1.toString(),"Direccion"); :} OPERADOR_ASIG OPERACION |
    SENTENCIA_BOOLEANA OPERADOR_OR_AND OPERACION 
;
IF_ELSE ::= IF Else {: parser.StartElse(); :} LlaveA SENTENCIA LlaveC {: parser.EndIf(); :}
;
WHILE ::= While {: parser.StartWhile(); :} ParentesisA SENTENCIA_BOOLEANA ParentesisC {: parser.TestWhile(); :} LlaveA SENTENCIA LlaveC {: parser.EndWhile(); :}
;
DO_WHILE ::= Do {: parser.contadorBreak++;
    parser.contadorContinue++; :} LlaveA SENTENCIA LlaveC While ParentesisA SENTENCIA_BOOLEANA ParentesisC PuntoComa {: parser.contadorBreak--;
    parser.contadorContinue--; :}
;
FOR ::= For {: parser.contadorBreak++;
    parser.contadorContinue++; :} ParentesisA SENTENCIA_FOR ParentesisC LlaveA SENTENCIA LlaveC {: parser.contadorBreak--;
    parser.contadorContinue--; :}
;
SENTENCIA_FOR ::=
    TIPO_DATO DECLARACION_LIST PuntoComa SENTENCIA_BOOLEANA PuntoComa LISTA_EXPRESIONES |
    DECLARACION_LIST PuntoComa SENTENCIA_BOOLEANA PuntoComa LISTA_EXPRESIONES
;
SWITCH ::= Switch {: parser.contadorBreak++; :} ParentesisA SENTENCIA_BOOLEANA ParentesisC LlaveA ETIQUETA_SWITCH LlaveC {: parser.contadorBreak--; :}
;
ETIQUETA_SWITCH ::=
    ETIQUETA_SWITCH Case OPERACION_SWITCH1 DosPuntos SENTENCIA |
    Case OPERACION_SWITCH1 DosPuntos SENTENCIA |
    ETIQUETA_SWITCH Default DosPuntos SENTENCIA |
    Default DosPuntos SENTENCIA |
    error DosPuntos |
    error PuntoComa
;
OPERACION_SWITCH1 ::= 
    OPERACION_SWITCH1 OPERADOR_ARITMETICO1 OPERACION_SWITCH2 | 
    OPERACION_SWITCH2
;
OPERACION_SWITCH2 ::= 
    OPERACION_SWITCH2 OPERADOR_ARITMETICO2 CONSTANT_FACTOR | 
    CONSTANT_FACTOR
;
CONSTANT_FACTOR ::= 
    ParentesisA OPERACION_SWITCH1 ParentesisC | 
    DATO_PREFIX ParentesisA OPERACION_SWITCH1 ParentesisC | 
    DATO_PREFIX Int |
    Int |
    DATO_PREFIX LiteralChar |
    LiteralChar
;
LISTA_EXPRESIONES ::= 
    EXPRESION |
    LISTA_EXPRESIONES Coma EXPRESION  
;
LISTA_IDENTIFICADORES ::= 
    Identificador:v1 {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.GuardarRS_Param(v1.toString(),v1right +1,v1left +1); :} |
    LISTA_IDENTIFICADORES Coma Identificador:v1 {: parser.BuscarSimbolo(v1.toString(),v1right +1,v1left +1, "Local"); 
    parser.GuardarRS_Param(v1.toString(),v1right +1,v1left +1); :}
;